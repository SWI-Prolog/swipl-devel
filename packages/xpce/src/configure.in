dnl Process this file with autoconf to produce a configure script.

AC_INIT(Pce.in)

AC_CONFIG_HEADER(config.h)
AC_SUBST(YACC)
AC_SUBST(NETLIBS)
AC_SUBST(PL)		dnl specified Prolog
AC_SUBST(PLBUILD)	dnl the one to use while building
AC_SUBST(PLNAME)	dnl the plain filename
AC_SUBST(PLLIBS)
AC_SUBST(PLBASE)
AC_SUBST(PLARCH)
AC_SUBST(BOOTPL)
AC_SUBST(PLINCL)
AC_SUBST(XLIB)
AC_SUBST(XINCLUDES)
AC_SUBST(XLIBS)
AC_SUBST(CXXLIBHACK)
AC_SUBST(PCEHOME)
AC_SUBST(CIFLAGS)
AC_SUBST(COFLAGS)
AC_SUBST(CWFLAGS)
AC_SUBST(CMFLAGS)
AC_SUBST(ETAGS)
AC_SUBST(ARCH)
AC_SUBST(OS)
AC_SUBST(TAR_CP_FLAGS)
AC_SUBST(ALLTARGET)
AC_SUBST(RTSUFFIX)
AC_SUBST(LD)
AC_SUBST(SO)
AC_SUBST(LDSOFLAGS)
AC_SUBST(SONAMEFLAG)
AC_SUBST(SOWHOLEARCHIVE)
AC_SUBST(CXXTARGETS)
AC_SUBST(GCCLIB)
AC_SUBST(SOEXTRAOBJ)
AC_SUBST(PLTARGET)
AC_SUBST(buildsubdir)
AC_SUBST(VMAJOR)
AC_SUBST(VMINOR)
AC_SUBST(VPATCH)
AC_SUBST(LDCONFIG)
AC_SUBST(AR)
AC_SUBST(MAKESHELL)
AC_SUBST(RSYNC)
AC_SUBST(QPENTRIES)
AC_SUBST(QPBIN)
AC_SUBST(QPPATH)
AC_SUBST(SP_PATH)
AC_SUBST(VWS)

dnl Link to X11.  Only Cygwin version doesn't do this

WITHX11=true
VWS=x11

RTSUFFIX=""
QPENTRIES=""
QPBIN="sun4-5"
QPPATH="/usr/quintus"
CXXLIBHACK=""

LD=ld
AC_PROG_CC
AC_PROG_RANLIB
AC_CHECK_PROGS(AR, ar /usr/ccs/bin/ar, ar)
AC_CHECK_PROGS(LDCONFIG, ldconfig, "true")
AC_CANONICAL_HOST
changequote(,)dnl
ARCH=`echo $host | sed 's/\([^-]*\)-[^-]*-[^-]*/\1/'`
OS=`echo $host | sed 's/[^-]*-[^-]*-\([^-]*\)/\1/'`
VMAJOR=`sed 's/\([0-9]*\).*/\1/' ../VERSION`
VMINOR=`sed 's/[0-9]*\.\([0-9]*\).*/\1/' ../VERSION`
VPATCH=`sed 's/[0-9]*\.[0-9]*\.\([0-9]*\).*/\1/' ../VERSION`
changequote([,])dnl

swipl=maybe
runtime=no

AC_ARG_WITH(pl, [  --without-pl		  Donot include SWI-Prolog],
	    [case "$withval" in
		yes)	swipl=yes ;;
		no)     swipl=no ;;
	     esac])
AC_ARG_ENABLE(shared, [  --enable-shared	  Prepare for shared lib],
	      [case "$enableval" in
		   yes) MAKE_SHARED_OBJECT=yes ;;
		   no)  MAKE_SHARED_OBJECT=no ;;
	       esac])
AC_ARG_ENABLE(sysld, [  --enable-sysld	  Use system ld, not gcc -shared],
	      [case "$enableval" in
		   yes) GCCLD=no ;;
		   no)  GCCLD=yes ;;
	       esac])
AC_ARG_ENABLE(cpp, [  --enable-cpp		  Include C++ interface],
	      [case "$enableval" in
		   yes) CONFIG_CXX=yes ;;
		   no)  CONFIG_CXX=no ;;
	       esac])
AC_ARG_ENABLE(runtime, [  --enable-runtime	  Runtime system only],
	      [case "$enableval" in
		   yes) AC_DEFINE(O_RUNTIME, 1, 
				  "Define if you want the runtime version")
		        runtime=yes
			RTSUFFIX=rt ;;
		   no)  runtime=no ;;
	       esac])
AC_ARG_ENABLE(mt, [  --enable-mt		  Enable Multi-threading],
	      [case "$enableval" in
		   yes) AC_DEFINE(_REENTRANT, 1,
				  "Define to prepare for multi-threading")
			MT=true
			;;
		   *)   MT=false
		        ;;
	       esac])


if test ! -z "$CIFLAGS"; then CFLAGS="$CIFLAGS $CFLAGS"; fi

# Sometimes this is wrong !
PWD=`pwd`

AC_MSG_CHECKING("Path to installation")
PCEHOME=`dirname $PWD`
AC_MSG_RESULT("$PCEHOME")

AC_MSG_CHECKING("Path relative to $PCEHOME")
buildsubdir=`basename $PWD`
AC_MSG_RESULT("$buildsubdir")

if test "$swipl" != "no"; then
  if test "$runtime" = "yes"; then
    if test -z "$PL"; then
      if test "$exec_prefix" = "NONE"; then
	if test "$prefix" = "NONE"; then
	  plprefix=/usr/local
	else
	  plprefix="$prefix"
	fi
      else
	plprefix="$exec_prefix"
      fi
      if test -r $plprefix/lib/rt-pl-*/bin/$ARCH-$OS/pl; then
        PL=$plprefix/lib/rt-pl-*/bin/$ARCH-$OS/pl
	AC_MSG_RESULT("PL=$PL")
      else
        AC_MSG_RESULT("No $plprefix/lib/rt-pl-*/bin/$ARCH-$OS/pl");
      fi
    fi
    plcandidates="plrt"
  else
    plcandidates="swipl swi-prolog pl"
  fi
  AC_CHECK_PROGS(PL, $plcandidates, "none")
  if test $PL = "none"; then
     if test "$swipl" = yes; then
        AC_ERROR("Cannot find SWI-Prolog. SWI-Prolog must be installed first")
     else
        swipl=no
     fi
  else
     swipl=yes
     if test -z "$PLINCL"; then
         AC_CHECKING("Running $PL -dump-runtime-variables")
         eval `$PL -dump-runtime-variables`
	 PLBUILD="$PL"
     else
         PLBUILD="`(cd ../../../$BUILDDIR && pwd)`/$PL"
     fi
  fi
  PLNAME=`basename $PL`
  if test "$swipl" = yes; then
    if test -z "$MAKE_SHARED_OBJECT" && test "$PLSHARED" = "yes"; then
       MAKE_SHARED_OBJECT=yes
    fi
    AC_MSG_RESULT("		PLBASE=$PLBASE")
    AC_MSG_RESULT("		PLARCH=$PLARCH")
    AC_MSG_RESULT("		PLLIBS=$PLLIBS")
    AC_MSG_RESULT("		PLLDFLAGS=$PLLDFLAGS")
    AC_MSG_RESULT("		PLSOEXT=$PLSOEXT")
    AC_MSG_RESULT("		MAKE_SHARED_OBJECT=$MAKE_SHARED_OBJECT")
    
    case "$PLARCH" in
	*)			LDFLAGS="$PLLDFLAGS" ;;
    esac
    ALLTARGET=xpce-pl
    PLTARGET=nosoall
  else
    ALLTARGET=xpce
  fi
else
  ALLTARGET=xpce
fi

if test "$MT" = true; then
  case "$OS" in
      *freebsd*)	AC_DEFINE(_THREAD_SAFE, 1,
				  [Define for threaded code on FreeBSD])
			;;
  esac
fi

dnl Frank C: find the pl development include dir, should be same
dnl version. Is up to the user?

AC_ARG_WITH(pl-include,[  --with-pl-include=DIR   find the pl include files in DIR],
	[ case $withval in
		no|yes) echo 'Specify dir for pl-include please (ignored)';;
		*)	PLINCL="$withval";;
	  esac
	],
	[
		if test -z "$PLINCL"; then
	 	  PLINCL="$PLBASE/include"
		fi
	])

dnl The following part is to decide for making a shared library or
dnl other dynamic linkable object.  This isn't easy and and is mostly
dnl done on the basis what I know to work for specific operating systems.
dnl How to make this any better?
dnl
dnl Only the sunos, solaris and linux version are supposed to really work.
dnl Rest needs to be validated.

if test -z "$MAKE_SHARED_OBJECT"; then
  case "$OS" in
    *solaris* | *sunos* | *linux* | *irix* | *osf* | *aix*)
      MAKE_SHARED_OBJECT=yes;;
  esac
fi

if test "$MAKE_SHARED_OBJECT" = "yes"; then
  PLTARGET=soall			 # Link XPCE dynamically to prolog
  BOOTPL="$PL"
  
  AC_MSG_CHECKING("Extension for shared objects")
  if test ! -z "$PLSOEXT"; then
    SO="$PLSOEXT"
  else
    SO=so
    case "$OS" in
      *aix*)	SO="o"
		  ;;
      *hpux*)	SO="sl"
		  ;;
    esac
  fi
  AC_MSG_RESULT("$SO")

  if test ! -z "$GCC"; then
    AC_MSG_CHECKING("Looking for -lgcc")
    if GCCLIB=`${CC-cc} -print-libgcc-file-name`; then
      AC_MSG_RESULT("$GCCLIB")
    else
cat > conftest.c <<EOF
main(){}
EOF
      ${CC-cc} -c conftest.c
      ${CC-cc} -v -o conftest conftest.o 2> conftest.out 1>/dev/null
      changequote(,)dnl
      GCCLIBD<IR=`grep lgcc conftest.out | sed 's/.*-L\([^ ]*gcc[^ ]*\).*/\1/'`
      changequote([,])dnl
      if test -r "$GCCLIBDIR/libgcc.a"; then
	GCCLIB="$GCCLIBDIR/libgcc.a"
	AC_MSG_RESULT("$GCCLIB")
      else
	AC_MSG_RESULT("no")
      fi
    fi
    rm -f conftest*
    CMFLAGS="$CMFLAGS -fPIC"

dnl See whether gcc supports building shared objects.  Some older versions
dnl recognise the flags, but don't behave appropirately.

    if test -z "$GCCLD"; then
    AC_MSG_CHECKING("whether ${CC-cc} -shared works")
    cat > conftest.c <<EOF
int
dummy()
{ return 1+1;
}
EOF
    ${CC-cc} -o conftest.$SO -fpic -shared conftest.c >/dev/null 2>&1
    if test -r conftest.$SO; then
      AC_MSG_RESULT("yes")
      LD=${CC-cc}
      LDSOFLAGS="-shared"
      GCCLD=yes
    else
      GCCLD=no
      AC_MSG_RESULT("no")
    fi
    rm -f conftest*
    fi

dnl GCC can be configured to use GNU-ld or native ld.  Check whether the
dnl relevant GNU-ld options are supported.    

    AC_MSG_CHECKING([whether ${CC-cc} -Wl,-soname works])
    cat > conftest.c <<EOF
int
dummy()
{ return 1+1;
}
EOF
    ${CC-cc} -o conftest.o -fpic -c conftest.c >/dev/null 2>&1
    ${CC-cc} -o conftest.$SO -shared -Wl,-soname,libconftest.$SO.1 conftest.o >/dev/null 2>&1
    if test -r conftest.$SO; then
      AC_MSG_RESULT("yes")
      SONAMEFLAG='-Wl,-soname,$(LIBXPCESOM)'
    else
      AC_MSG_RESULT("no")
    fi
    rm -f conftest*

    AC_MSG_CHECKING([whether ${CC-cc} -Wl,-whole-archive works])
    cat > conftest1.c <<EOF
int
dummy1()
{ return 1+1;
}
EOF
    cat > conftest2.c <<EOF
int
dummy2()
{ return 1+1;
}
EOF
    ${CC-cc} -o conftest1.o -fpic -c conftest1.c >/dev/null 2>&1
    ${CC-cc} -o conftest2.o -fpic -c conftest2.c >/dev/null 2>&1 
    ${AR} ru conftest.a conftest1.o conftest2.o
    ${RANLIB} conftest.a
    ${CC-cc} -o conftest.$SO -shared -Wl,-whole-archive conftest.a >/dev/null 2>&1
    if test -r conftest.$SO; then
      AC_MSG_RESULT("yes")
      SOWHOLEARCHIVE="-Wl,-whole-archive"
    else
      AC_MSG_RESULT("no")
      SOWHOLEARCHIVE="-u pceSend -u pceOpen"
    fi
    rm -f conftest*
  fi

  case "$OS" in
    *solaris* | *sunos5*)
	if test "$GCCLD" = "no"; then
	    LD=/usr/ccs/bin/ld
	    LDSOFLAGS="-G"
	    SONAMEFLAG="-h $(LIBXPCESOM)"
	    SOWHOLEARCHIVE="-u pceSend -u pceOpen"
	    if "$GXX" != "NONE"; then
	      LDSOFLAGS="$LDSOFLAGS -u __builtin_new"
	    fi
	fi
	if test -z "$GCC"; then
	    CMFLAGS="$CMFLAGS -K"
	fi
	;;
    *sunos*)
	if test "$GCCLD" = "no"; then
	    LD=ld
	    LDSOFLAGS="-assert nodefinitions"
	    SOWHOLEARCHIVE="-u _pceSend -u _pceOpen"
	    SONAMEFLAG=""
	    SOEXTRAOBJ=Initialize.o
	fi
	if test -z "$GCC"; then
	    CMFLAGS="$CMFLAGS -K"
	fi
	;;
    *linux*)
	CXXLIBHACK="-lc -Wl,-Bstatic"
	;;
    *irix*)
	;;
    *osf*)
	if test "$GCCLD" = "no"; then
	    LD=ld
	    LDSOFLAGS="-shared -expect_unresolved \*"
	fi
	if test -z "$GCC"; then
	    CMFLAGS="$CMFLAGS -std"
	fi
	AC_DEFINE(VARIABLE_POINTER_OFFSET, 1,
		  "Define if position of data-segment isn't more or less constant")
	;;
    *aix*)
        LDSOFLAGS="-bI:$PLBASE/include/SWI-Exports"
	undefine([__unix__])
	AC_DEFINE(__unix__, 1, "Define if your system is Unix-like")
	QPENTRIES=entries.c
	;;
    *hpux*)
    	SO="sl"
	LDSOFLAGS="-b"
	;;
    *cygwin*)
	AC_MSG_CHECKING("Setup for CygWin")
	WITHX11=false
	VWS=msw
	AC_DEFINE(WIN32_GRAPHICS, 1, 
		  "Define to use WIN32 graphics interface")
	AC_DEFINE(XPM_NO_X, 1, "Define to use XPM without X11 (Cygwin)")
	LIBS="-L/usr/lib/noX $LIBS"
	XINCLUDES=/home/jan/cygwin/include
	XLIBS="-lwsock32 -lcomdlg32 -lkernel32 -luser32 -lgdi32"
	XLIB=/home/jan/cygwin/lib
	LD=plld
	GCCLIB=""
	LDSOFLAGS="-shared"
	CMFLAGS=`echo $CMFLAGS | sed 's/-fPIC//'`
	AC_MSG_RESULT(ok)
	;;
    *darwin*)
	LD=plld
	LDSOFLAGS="-shared"
	GCCLIB=""
	CMFLAGS="$CMFLAGS -no-cpp-precomp"
	AC_DEFINE(__unix__)
	;;
    *)
	;;
  esac
else
  BOOTPL=xpce
fi

dnl %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
dnl % Begin Quintus specific configuration
dnl %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

if test -d ../quintus/src; then

AC_CHECK_PROGS(QPproduct, qpc qld quintus prolog, ":")
if test $QPproduct != ":"; then
    AC_CHECK_PROGS(QGETPATH, qgetpath, ":")
    if test $QGETPATH != ":"; then
	AC_PATH_PROG(QPPRODUCT, $QPproduct)
	AC_MSG_CHECKING("Quintus directory")
	QPPATH=`$QGETPATH -q $QPPRODUCT`
	AC_MSG_RESULT($QPPATH)
	AC_MSG_CHECKING("Quintus host type identifier")
	QPBIN=`$QGETPATH -h $QPPRODUCT`
	QPBIN=`basename $QPBIN`
	AC_MSG_RESULT($QPBIN)
    fi
fi


fi

dnl %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
dnl % End Quintus specific configuration
dnl %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

dnl %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
dnl % Begin SICStus specific configuration
dnl %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

if test -d ../sicstus/src; then
AC_CHECK_PROGS(SICSTUS, sicstus, ":")
if test $SICSTUS != ":"; then
cat > conftest.sh << _EOS_
$SICSTUS << _EOF_
absolute_file_name('\\\$SP_PATH', X),
write(X), nl, halt(0).
halt(1).
_EOF_
_EOS_
SP_PATH=`sh conftest.sh 2>/dev/null`
rm -f conftest.sh

if test ! -d "$SP_PATH"; then SP_PATH=""; fi
fi
fi

dnl %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
dnl % End SICStus specific configuration
dnl %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


if test "$runtime" = yes; then
  PLTARGET=rt-$PLTARGET
fi

AC_CHECK_PROGS(MAKESHELL, bash sh, "/bin/sh")
AC_CHECK_PROGS(MAKE, gmake make, "make")
AC_CHECK_PROGS(ETAGS, emacs-etags etags ctags, ":")
AC_CHECK_PROGS(RSYNC, rsync ./rsync.sh)
if test "$CONFIG_CXX" = "yes"; then
  AC_CHECK_PROGS(GXX, g++, "NONE")
  if test "$GXX" != "NONE"; then
    AC_DEFINE(O_CPLUSPLUS, 1,
	      "Define if you want to include the C++ interface")
    CXXTARGETS=cpp.o
    CXX="$GXX"
  else
    CXXTARGETS=""
  fi
fi
AC_PROG_LN_S
AC_PROG_CPP
AC_PROG_CXX
AC_ISC_POSIX
AC_HEADER_STDC
if test ! -z "$GCC"; then
    COFLAGS="-O2 -fno-strict-aliasing"
    if test "$ac_cv_header_stdc" = "yes" || test "$syslibh" = "yes"; then
        CWFLAGS=-Wall
    else
	CWFLAGS=
    fi
else
    COFLAGS=-O
    CWFLAGS=
fi
CFLAGS="$CMFLAGS"
AC_C_INLINE
AC_FUNC_ALLOCA
AC_C_BIGENDIAN

AC_CHECK_PROGS(YACC, bison yacc, yacc)
if test $YACC = "bison"; then YACC="bison -y"; fi

AC_CHECK_LIB(socket, socket,
	     [NETLIBS="$NETLIBS -lsocket"; LIBS="$LIBS -lsocket"]
	     AC_DEFINE(HAVE_LIBSOCKET, 1,
		       "Define if you have the socket library (-lsocket)."))
AC_CHECK_LIB(nsl, t_bind,
	     [NETLIBS="$NETLIBS -lnsl"]
	     AC_DEFINE(HAVE_LIBNSL, 1,
		       "Define if you have the nsl library (-lnsl)."))

AC_CHECK_HEADERS(unistd.h string.h memory.h sys/time.h sys/file.h pwd.h)
AC_CHECK_HEADERS(sys/select.h sys/param.h malloc.h sys/resource.h stropts.h)
AC_CHECK_HEADERS(frame.h sys/timeb.h sys/times.h siginfo.h bstring.h)
AC_CHECK_HEADERS(sys/socketvar.h conio.h sys/access.h termios.h termio.h)
AC_CHECK_HEADERS(sched.h)

AC_CHECK_FUNCS(vsscanf on_exit memmove select popen strerror timelocal)
AC_CHECK_FUNCS(getdtablesize socket fork ftime getpid select getlogin)
AC_CHECK_FUNCS(getcwd setsid grantpt gethostname fstat gettimeofday)
AC_CHECK_FUNCS(rename atexit on_exit tmpnam sysinfo sigaction getpwnam)
AC_CHECK_FUNCS(shutdown mkstemp sched_yield mktime timegm)
AC_HEADER_TIME
AC_HEADER_DIRENT
AC_C_CHAR_UNSIGNED

if test "$ac_cv_func_vsscanf" = "yes"; then
AC_MSG_CHECKING("cast to va_list")
AC_TRY_RUN(
[
#include <stdarg.h>
main()
{ long v[2];
  void *ptr[2];

  ptr[0] = &v[0];
  ptr[1] = &v[1];
  vsscanf("34 45", "%d%d", (va_list)ptr);
  if ( v[0] == 34 && v[1] == 45 )
    exit(0);

  exit(1);
}], AC_DEFINE(HAVE_CAST_VA_LIST, 1,
	      "Define if we can cast to va_list (say no if you are not sure)")
    AC_MSG_RESULT("yes"),
    AC_MSG_RESULT("no"),
    AC_MSG_RESULT("assuming no"))
fi

if test "$ac_cv_func_ftime" != "yes"; then
AC_MSG_CHECKING("tm_gmtoff")
AC_TRY_COMPILE(
[
#include <sys/types.h>
#include <time.h>
],
[ struct tm *tm;
  int i;
  
  i = tm->tm_gmtoff;
], AC_DEFINE(HAVE_TM_GMTOFF, 1,
	     "Define if struct tm has tm_gmtoff field")
   AC_MSG_RESULT("yes"),
   AC_MSG_RESULT("no"))
fi

AC_TYPE_SIGNAL
AC_MSG_CHECKING("for BSD signal handlers")
AC_TRY_RUN(
[
#include <stdio.h>
#include <sys/types.h>
#include <sys/stat.h>
#include <signal.h>
#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif
static int signalled;
static RETSIGTYPE catch (s) int s; { signalled = 1; }
main()
{ signal(SIGINT, catch);
  kill(getpid(), SIGINT);
  while(!signalled)
    ;
  if ( signal(SIGINT, catch) == catch )
    exit(0);
  exit(1);
}], AC_DEFINE(BSD_SIGNALS, 1,
	      "Define if you have BSD signals (i.e. handler does not reset)")
    AC_MSG_RESULT("BSD signals"),
    AC_MSG_RESULT("Posix signals"),
    AC_MSG_RESULT("assuming Posix signals"))

dnl The code below checks whether we can use siginfo () to set a signal
dnl handler that is passed the child's pid.  This code works on Solaris,
dnl but on irix, it compiles, but the handler is not passed the proper
dnl information.  Therefore we need to run the program.  If this fails,
dnl the system will fall back to use signal() and waitpid(), but this
dnl is more clumsy, and fails to work properly on Solaris.

AC_MSG_CHECKING("for usable siginfo")
AC_TRY_RUN(
[
#include <stdio.h>
#include <signal.h>
#include <siginfo.h>
#include <stdlib.h>
#include <sys/types.h>
#ifdef HAVE_STRING_H
#include <string.h>
#endif
#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif

#ifndef SA_NOMASK
#define SA_NOMASK 0
#endif

int child;

static void
child_changed(int sig, siginfo_t *info, void *uctx)
{ fprintf(stderr, "child_changed() called, child = %d, info = %p\n",
	  child, info);
  if ( info && info->si_pid == child )
    exit(0);

  exit(1);
}

int
main()
{ struct sigaction action, oaction;

  memset((char *) &action, 0, sizeof(action));
  action.sa_sigaction = child_changed;
  action.sa_flags     = SA_SIGINFO|SA_NOMASK|SA_RESTART;
  sigaction(SIGCHLD, &action, &oaction);

  if ( (child = fork()) == 0 )
  { sleep(1);
    exit(0);
  }

  sleep(8);
  return 1;
}
], AC_DEFINE(USE_SIGINFO, 1,
	     "Define if sigaction() supports SA_SIGINFO and passes thechild pid in info")
   AC_MSG_RESULT(yes),
   AC_MSG_RESULT(no),
   AC_MSG_RESULT(assuming no))


AC_MSG_CHECKING("for union wait")
if test "$GCC" = yes; then
    ac_oldcflags="$CFLAGS"
    CFLAGS="$CFLAGS -Werror"
fi
AC_TRY_COMPILE(
[
#include <sys/wait.h>
],
[ union wait status;
  waitpid(1, &status, WNOHANG);
], AC_DEFINE(UNION_WAIT, 1, 
	     "Define if wait() uses union wait* for the 2nd argument.")
   AC_MSG_RESULT(yes),
   AC_MSG_RESULT(no))
if test "$GCC" = "yes"; then CFLAGS="$ac_oldcflags"; fi

AC_MSG_CHECKING("for tagged lvalues")
AC_TRY_COMPILE(
[
],
[ int x;
  *(char *)&x = 'a';
  exit(0);
], AC_DEFINE(TAGGED_LVALUE, 1, 
	     "Define if (type)pointer = value is allowed")
   AC_MSG_RESULT(yes),
   AC_MSG_RESULT(no))

AC_MSG_CHECKING("Checking struct termios.c_line")
AC_TRY_COMPILE(
[
#include <termios.h>
],
[ struct termios buf;
  buf.c_line = 0;
  exit(0);
], AC_DEFINE(TERMIOS_HAS_C_LINE, 1, 
	     "Define if struct termios has a member c_line")
   AC_MSG_RESULT(yes),
   AC_MSG_RESULT(no))


AC_MSG_CHECKING('ioctl(line, I_PUSH, "ttcompat")')
AC_TRY_RUN(
[
#include <stdio.h>
#include <fcntl.h>
#include <stropts.h>
main()
{ int line;

  if ( (line = open("/dev/tty", O_RDWR)) >= 0 &&
       ioctl(line, I_PUSH, "ttcompat") == 0 )
    exit(0);
  exit(1);
}
], AC_DEFINE(HAVE_TTCOMPAT, 1,
	     "Define if iocll(tty, I_PUSH, "ttcompat") works on your machine")
   AC_MSG_RESULT(yes),
   AC_MSG_RESULT(no),
   AC_MSG_RESULT(assuming no))

if test "$WITHX11" = true; then
BEFOREXLIBS="$LIBS"

AC_PATH_X

if test "$static_x_libraries" = "yes"; then
    XLIBS="$XLIB/libXt.a $XLIB/libX11.a"
else
    XLIBS="-lXt -lX11"
fi

if test "$x_libraries" = ""; then
    XLIB=/usr/lib
else
    XLIB="$x_libraries"
fi
if test "$x_includes" = ""; then
XINCLUDES=/usr/include
else
XINCLUDES="$x_includes"
fi

ac_oldlibs="$LIBS"
if test ! -z "$x_libraries"; then
  LIBS="-L$x_libraries $LIBS"
fi
XSMLIBS=""
AC_CHECK_LIB(SM, main,
	     XSMLIBS="-lSM -lICE",
	     ,
	     -lX11 -lXt -lICE)
XLIBS="$XLIBS $XSMLIBS"
echo "	XLIBS=$XLIBS"
if test ! -z "$x_libraries"; then
  LIBS="-L$x_libraries $XLIBS $ac_oldlibs"
else
  LIBS="$XLIBS $ac_oldlibs"
fi

dnl The SunOS 4.1.x linker is broken.  This hacks around this problem
dnl the real linking hacks around it in yet another way ...

case "$OS" in
  *sunos*) LIBS="$BEFOREXLIBS -lX11 $XSMLIBS";;
esac

CPPFLAGS="-I$XINCLUDES ${CPPFLAGS}"
CIFLAGS="-I$XINCLUDES ${CIFLAGS}"

fi dnl WITHX11

dnl Check the critical but not very standard libXpm and libjpeg libraries
dnl and make sure they required headers can be found.
dnl
dnl NOTE: If we have to add a -L, it should be *before* the -l for some
dnl linkers

XLIBDIRS=""

AC_ARG_WITH(xpm,[  --with-xpm=<dir>	  Location of Xpm include/lib],
	[case $withval in 
		no|yes)	echo 'Specify dir for XPM please';
			exit 1 ;;
		*)	XLIBDIRS="-L${withval}/lib $XLIBDIRS"
		        CFLAGS="-I${withval}/include ${CFLAGS}"
			CPPFLAGS="-I${withval}/include ${CPPFLAGS}"
			CIFLAGS="-I${withval}/include ${CIFLAGS}"
			echo "LIBS=${LIBS}"
	esac])
AC_ARG_WITH(jpeg,[  --with-jpeg=<dir>	  Location of JPEG include/lib],
	[case $withval in 
		no|yes)	echo 'Specify dir for JPEG please';
			exit 1 ;;
		*)	XLIBDIRS="-L${withval}/lib $XLIBDIRS"
			CPPFLAGS="-I${withval}/include ${CPPFLAGS}"
			CIFLAGS="-I${withval}/include ${CIFLAGS}"
			echo "LIBS=${LIBS}"
	esac])

dnl Clear the cache if the answer is no so re-running configure
dnl after fixing the problem works fine without removing the cache file

if test "$ac_cv_lib_Xpm_main" != yes; then unset ac_cv_lib_Xpm_main; fi
if test "$ac_cv_lib_jpeg_main" != yes; then unset ac_cv_lib_jpeg_main; fi
if test "$ac_cv_header_X11_xpm_h" != yes; then unset ac_cv_header_X11_xpm_h; fi
if test "$ac_cv_header_jpeglib_h" != yes; then unset ac_cv_header_jpeglib_h; fi
if test "$ac_cv_header_jerror_h" != yes; then unset ac_cv_header_jerror_h; fi
	
LIBS="$XLIBDIRS $LIBS"

AC_CHECK_LIB(jpeg, main,
	     [XLIBS="-ljpeg $XLIBS"]
	     AC_DEFINE(HAVE_LIBJPEG, 1,
		       "Define if you have the jpeg library (-ljpeg)"))

AC_CHECK_LIB(Xpm, main,
	     [XLIBS="-lXpm $XLIBS"]
	     AC_DEFINE(HAVE_LIBXPM, 1,
		       "Define if you have the Xpm library (-lXpm)."))

XLIBS="$XLIBDIRS $XLIBS"

if test "$ac_cv_lib_Xpm_main" != "yes"; then
cat << _EOM_
*****************************************************************
* ERROR: Could not find library -lXpm.				*
* Please install the XPM library and rerun configure.		*
* The primary source for libXpm is at:				*
*								*
*     http://www-sop.inria.fr/koala/lehors/xpm.html		*
*****************************************************************
_EOM_
exit 1
fi

AC_CHECK_HEADERS(X11/xpm.h)
if test "$ac_cv_header_X11_xpm_h" != yes; then
cat << _EOM_
*****************************************************************
* ERROR: Could not find <X11/xpm.h>				*
* Please install the XPM development files and rerun configure.	*
* The primary source for libXpm is at:				*
*								*
*     http://www-sop.inria.fr/koala/lehors/xpm.html		*
*****************************************************************
_EOM_
exit 1
fi

if test "$ac_cv_lib_jpeg_main" != "yes"; then
cat << _EOM_
*****************************************************************
* ERROR: Could not find library -ljpeg.				*
* Please install the JPEG library and rerun configure.		*
* The primary source for libjpeg is at:				*
*								*
*     http://www.ijg.org/					*
*****************************************************************
_EOM_
exit 1
fi

AC_CHECK_HEADERS(jpeglib.h jerror.h)
if test "$ac_cv_header_jpeglib_h" != yes -o \
        "$ac_cv_header_jerror_h" != yes; then
cat << _EOM_
*****************************************************************
* ERROR: Could not find <jpeglib.h> or <jerror.h>		*
* Please install the JPEG development files and rerun configure.*
* The primary source for libjpeg is at:				*
*								*
*     http://www.ijg.org/					*
*****************************************************************
_EOM_
exit 1
fi

if test "$WITHX11" = true; then
  AC_CHECK_FUNCS(XtPopupSpringLoaded XmIsMotifWMRunning XInitThreads)
  LIBS="$ac_oldlibs"
fi

AC_MSG_CHECKING("memory model")
echo '#include "confdefs.h"' > conftest.c
cat $srcdir/test/m-model.c >> conftest.c
(eval $ac_link) 2>&5
if test -s conftest && eval `./conftest`; then
    msg=
    if test ! -z "$POINTER_OFFSET"; then
	AC_DEFINE_UNQUOTED(POINTER_OFFSET, $POINTER_OFFSET,
			   "Define if data-pointer is in high memory")
	msg="$msg POINTER_OFFSET=$POINTER_OFFSET"
    fi
    if test -z "$msg"; then msg="yes"; fi
    AC_MSG_RESULT("$msg")
else
    AC_MSG_RESULT("Could not compile m-model.c")
fi
rm -rf conftest*

dnl See which parts of the sources are there, and check the Makefiles we
dnl need to update.  You cannot call AC_OUTPUT more then once!

Makefiles="Makefile ../xpce.spec"
Makedirs="../CXX/demo ../quintus/src ../prolog/pwboot ../sicstus/src ../prolog/lib"
OptionalMakefiles="../pl/src/Makefile.bin"
for d in $Makedirs; do
    if test -r $d/Makefile.in; then
	Makefiles="$Makefiles $d/Makefile"
    fi
done
for f in $OptionalMakefiles; do
    if test -r $f.in; then
	Makefiles="$Makefiles $f"
    fi
done

dnl autoconf 2.5.3 doesn't handle relative srcdir properly.  We'll hack
dnl around making this variable global.

srcdir=`cd $srcdir && pwd`

AC_OUTPUT($Makefiles)

