dnl Process this file with autoconf to produce a configure script.

AC_INIT(install-sh)
AC_CONFIG_HEADER(config.h)

AC_SUBST(NETLIBS)
AC_SUBST(PL)
AC_SUBST(PLLIBS)
AC_SUBST(PLBASE)
AC_SUBST(PLARCH)
AC_SUBST(PLINCL)
AC_SUBST(COFLAGS)
AC_SUBST(CWFLAGS)
AC_SUBST(CMFLAGS)
AC_SUBST(ETAGS)
AC_SUBST(LD)
AC_SUBST(SO)
AC_SUBST(LDSOFLAGS)
AC_SUBST(TARGETS)
AC_SUBST(PLTARGETS)

AC_ARG_ENABLE(mt, [  --enable-mt		  Enable Multi-threading],
	      [case "$enableval" in
		   yes) AC_DEFINE(_REENTRANT)
			;;
		   *)  ;;
	       esac])

if test -z "$PLINCL"; then
plcandidates="pl swipl swi-prolog"
AC_CHECK_PROGS(PL, $plcandidates, "none")
AC_CHECK_PROGS(PLLD, plld, "none")
if test $PLLD = "none"; then
   AC_ERROR("Cannot find SWI-Prolog plld utility. SWI-Prolog must be installed first")
fi
if test $PL = "none"; then
   AC_ERROR("Cannot find SWI-Prolog. SWI-Prolog must be installed first")
else
   AC_CHECKING("Running $PL -dump-runtime-variables")
   eval `$PL -dump-runtime-variables`
fi
PLINCL=$PLBASE/include
AC_MSG_RESULT("		PLBASE=$PLBASE")
AC_MSG_RESULT("		PLARCH=$PLARCH")
AC_MSG_RESULT("		PLLIBS=$PLLIBS")
AC_MSG_RESULT("		PLLDFLAGS=$PLLDFLAGS")
AC_MSG_RESULT("		PLSHARED=$PLSHARED")
AC_MSG_RESULT("		PLSOEXT=$PLSOEXT")
else
PLLD=../../$BUILDDIR/plld
fi

CC=$PLLD
LD=$PLLD
LDSOFLAGS=-shared
CMFLAGS=-fpic

SO="$PLSOEXT"

TARGETS="unix.$SO socket.$SO cgi.$SO memfile.$SO files.$SO"

AC_CHECK_PROGS(MAKE, gmake make, "make")
AC_CHECK_PROGS(ETAGS, etags ctags, ":")
AC_PROG_INSTALL
AC_ISC_POSIX
AC_HEADER_STDC
CFLAGS="$CMFLAGS"
AC_FUNC_ALLOCA
AC_C_BIGENDIAN

if test ! -z "$GCC"; then
    COFLAGS="-O2 -fno-strict-aliasing"
    CWFLAGS=-Wall
else
    COFLAGS=-O
    CWFLAGS=
fi

AC_CHECK_LIB(socket, socket,
	     [NETLIBS="$NETLIBS -lsocket"; LIBS="$LIBS -lsocket"]
	     AC_DEFINE(HAVE_LIBSOCKET))
AC_CHECK_LIB(nsl, t_bind,
	     [NETLIBS="$NETLIBS -lnsl"]
	     AC_DEFINE(HAVE_LIBNSL))
AC_CHECK_LIB(crypt, crypt)
AC_CHECK_LIB(rfc822, rfc822t_alloc)
AC_MSG_CHECKING(for -lrfc2045)
oldlibs="$LIBS"
LIBS="-lrfc2045 $LIBS"
AC_TRY_LINK(
void
rfc2045_error(const char *errmsg)
{ 
},,
  AC_MSG_RESULT(yes)
  have_rfc2045=yes,
  AC_MSG_RESULT(no)
  have_rfc2045=no
  LIBS="$oldlibs")


if test "$ac_cv_lib_crypt_crypt" = yes; then
  TARGETS="$TARGETS crypt.$SO"
else
  echo "WARNING: could not find crypt; dropping crypt.pl from targets"
fi
if test "$ac_cv_lib_rfc822_rfc822t_alloc" = yes &&
   test "$have_rfc2045" = yes; then
  TARGETS="$TARGETS mime.$SO"
else
  echo "WARNING: could not find -lrfc822 and/or -lrfc2045; dropping mime.pl from targets"
fi

AC_MSG_CHECKING(h_errno)
AC_TRY_COMPILE(
[
#include <sys/types.h>
#include <sys/socket.h>
#include <netdb.h>
#include <netinet/in.h>
],
[ int x = h_errno;
], AC_DEFINE(HAVE_H_ERRNO)
   AC_MSG_RESULT(yes),
   AC_MSG_RESULT(no))

AC_CHECK_HEADERS(malloc.h alloca.h unistd.h sys/time.h fcntl.h utime.h)
AC_CHECK_HEADERS(execinfo.h)

AC_CHECK_FUNCS(setsid strerror setitimer utime)

with_time=yes
AC_ARG_WITH(time, [  --without-time		  Exclude alarm library],
	    [case "$withval" in
		yes)	with_time=yes ;;
		no)     with_time=no ;;
	     esac])

if test "$with_time" = yes; then
  if test "$ac_cv_func_setitimer" = "yes"; then
    TARGETS="$TARGETS time.$SO"
  else
    echo "WARNING: no setitimer(); dropping time.pl from targets"
  fi
fi

PLTARGETS=`echo $TARGETS | sed "s/\.$SO/.pl/g"`

AC_OUTPUT(Makefile)




